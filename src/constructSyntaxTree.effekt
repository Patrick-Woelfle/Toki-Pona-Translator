module constructSyntaxTree

import src/syntaxTree
import src/additionalListFunctions
import src/effects


/// Takes a Toki Pona sentence and turns it into a syntax tree
def constructSyntaxTree(sentence: List[String]): SyntaxTree / {ambiguityVerbOnlyLi} = {
  var tree: SyntaxTree = Sentence([], [])
  try {
    sentence.findAndRemoveSubjects().findAndRemovePredicates()
    return tree
  } with append { n => 
    tree = appendToSentence(tree, n)
    resume(())
  } with sentenceError { msg => 
    println("SENTENCE ERROR: " ++ msg)
    return Empty()
  } 
}


/// Adds a node at the right place in a syntax tree
def appendToSentence(tree: SyntaxTree, node: SyntaxTree): SyntaxTree = {
  tree match {
    case Sentence(subjects, predicates) => {
      node match {
        case SubjectLeaf(head, modifiers) => {
          return Sentence(subjects.append([node]), predicates)
        }
        case Predicate(verb, objects, prepositions) => {
          return Sentence(subjects, predicates.append([node]))
        }
        case _ => return Empty()
      }
    }
    case _ => return Empty()
  }
}


/// Takes a Toki Pona sentence, finds, removes and adds them to the syntax tree
def findAndRemoveSubjects(sentence: List[String]): List[String] / { append, sentenceError } = {
  if (sentence.subjectOnlyMiOrSina()) {
    do append(SubjectLeaf(sentence.take(1), Nil()))
    return sentence.drop(1)
  }
  else {
    sentence.getIndexOf("en") match {
      case Some(index) => {
        do append(SubjectLeaf(sentence.take(1), createModifiers(sentence.slice(1, index))))
        return findAndRemoveSubjects(sentence.drop(index + 1))
        }
      case None() => {
        sentence.getIndexOf("li") match {
          case Some(index) => {
            do append(SubjectLeaf(sentence.take(1), createModifiers(sentence.slice(1, index))))
            return sentence.drop(index + 1)
          }
          case None() => {
            do append(SubjectLeaf(sentence.take(1), createModifiers(sentence.drop(1))))
            return []
          }
        }
      }
    }
  }
}


/// Checks if the Subject is only Mi or Sina
/// TODO: Check if li is later in sentence
def subjectOnlyMiOrSina(sentence: List[String]): Bool / sentenceError = {
  sentence match {
    case Cons("mi", Cons("li", _)) => do sentenceError("If mi is subject, li is not needed"); false
    case Cons("mi", Cons("en", _)) => false
    case Cons("mi", Cons("ala", _)) => false
    case Cons("mi", notEn) => true
    case Cons("sina", Cons("li", _)) => do sentenceError("If sina is subject, li is not needed"); false
    case Cons("sina", Cons("en", _)) => false
    case Cons("sina", Cons("ala", _)) => false
    case Cons("sina", notEn) => true
    case _ => false
  }
}


// Takes a Toki Pona sentence without the subject and finds, removes and adds the predicates to the Syntax Tree
def findAndRemovePredicates(sentence: List[String]): List[String] / {append, sentenceError, ambiguityVerbOnlyLi} = {
  val endOfVerb: Int = {
    sentence.getIndexOf(["lon", "kepeken", "tawa", "tan", "sama", "li", "e"]) match {
      case Some(index) => index
      case None() => sentence.size()
    }
  }
  val endOfPreverbs: Int = {
    sentence.getIndexOf(["wile", "sona", "awen", "kama", "ken", "lukin"]) match {
      case Some(index) => {
        sentence match {
          case Cons(_, Cons("ala", _)) => 2
          case _ => {
            if (endOfVerb > 1) 1 
            else 0
          }
        }
      }
      case None() => 0
    }
  }
  val endOfPredicate = {
    sentence.getIndexOf("li") match {
      case Some(index) => index
      case None() => sentence.size()
    }
  } 
  val endOfObjects = {
    sentence.getIndexOf(["lon", "kepeken", "tawa", "tan", "sama", "li"]) match {
      case Some(index) => index
      case None() => endOfPredicate
    }
  }
  val objects: List[SyntaxTree] = splitObjects(sentence.slice(endOfVerb + 1, endOfObjects))
  val verb: SyntaxTree = VerbLeaf(sentence.take(endOfPreverbs), sentence.slice(endOfPreverbs, endOfPreverbs + 1), createModifiers(sentence.slice(endOfPreverbs + 1, endOfVerb)))
  val prepositions: List[SyntaxTree]  = splitPrepositions(sentence.slice(endOfObjects, endOfPredicate))
  val predicate = {
    if (objects.size() == 0 and endOfPreverbs == 0 and endOfVerb != 0) { // If there aren't any objects or preverbs, the verb can also be interpeted as the object
      if (do ambiguityVerbOnlyLi()) {
        Predicate(verb, objects, prepositions)
      } else {
        Predicate(Empty(), [ObjectLeaf(sentence.take(1), createModifiers(sentence.slice(1, endOfObjects)))], prepositions)
      }
    }
    else {
      Predicate(verb, objects, prepositions)
    }
  }
  do append(predicate)
  if(endOfPredicate == sentence.size()) return []
  else return findAndRemovePredicates(sentence.drop(endOfPredicate + 1))
}


/// Takes a list of Objects split by prepositions and turns them into Prepositions Leafs
def splitPrepositions(sentence: List[String]): List[SyntaxTree] = {
  var output: List[SyntaxTree] = []
  try {
    def splitPrepositionsHelper(sentence: List[String]): Unit / append = {
      if (sentence.size == 0) {
        return ()
      }
      else {
        sentence.drop(1).getIndexOf(["lon", "kepeken", "tawa", "tan", "sama"]) match {
          case Some(index) => {
            do append(PrepositionsLeaf(sentence.take(1), sentence.slice(1, 2), createModifiers(sentence.slice(2, index + 1))))
            splitPrepositionsHelper(sentence.drop(index + 1))
            return ()
          }
          case None() => {
            do append(PrepositionsLeaf(sentence.take(1), sentence.slice(1, 2), createModifiers(sentence.drop(2))))
          }
        }
      } 
    }
    splitPrepositionsHelper(sentence)
    return output
  } with append { n => 
    output = output.append([n])
    resume(())
  }
}


/// Takes a list of objects split by "e" and turns them into Object Leafs
def splitObjects(sentence: List[String]): List[SyntaxTree] = {
    var output: List[SyntaxTree] = []
  try {
    def splitObjectsHelper(sentence: List[String]): Unit / append = {
      if (sentence.size == 0) {
        return ()
      }
      else {
        sentence.drop(1).getIndexOf("e") match {
          case Some(index) => {
            do append(ObjectLeaf(sentence.take(1), createModifiers(sentence.slice(1, index + 1))))
            splitObjectsHelper(sentence.drop(index + 2))
            return ()
          }
          case None() => {
            do append(ObjectLeaf(sentence.take(1), createModifiers((sentence.drop(1)))))
          }
        }
      } 
    }
    splitObjectsHelper(sentence)
    return output
  } with append { n => 
    output = output.append([n])
    resume(())
  }
}

def createModifiers(words: List[String]): List[SyntaxTree] = {
  if (words.size() == 0) return []
  else {
    words.getIndexesOf("pi") match {
      case Cons(0, Cons(index, _)) => return Cons(Modifier(words.drop(1).take(1), words.slice(2, index)), createModifiers(words.drop(index)))
      case Cons(0, _) => return [Modifier(words.drop(1).take(1), words.drop(2))]
      case _ => return Cons(Modifier(words.take(1), []), createModifiers(words.drop(1)))
    }
  }
}


def main(): Unit = {
  try {
    createModifiers(["i", "si", "li", "jan", "pi", "tomo", "ja", "pi", "moku", "a", "b", "pi", "c", "d"]).foreach { x => printSyntaxTree(x) }
    printSyntaxTree(constructSyntaxTree(split("jan li pali pilin soweli e sitelen pi sitelen sitelen e lipu e kalama musi kepeken ilo wawa", " ")))
  } with ambiguityVerbOnlyLi {
    resume(true)
  }
}