module main

import src/constructSyntaxTree
import src/deconstructSyntaxTree
import src/effects
import io/console
import src/convertSyntaxTree
import src/syntaxTree
import io/filesystem
import io/error


def translateSentenceHelper(sentence: String): String / { ambiguity, ambiguityVerbOnlyLi }  = {
  deconstructSyntaxTree(convertSyntaxTree(constructSyntaxTree(split(sentence, " "))))
}


/// Translate a toki pona sentence to english, giving out all possible translations
def translateSentence(sentence: String): List[String] = {
  var output = []
  try {
    try {
      do escapeContext(translateSentenceHelper(sentence))
    } with ambiguity{ ambiguities =>
      ambiguities.foreach[(String, AmbiguityTypes)] { x => resume(x.first)}
    } with ambiguityVerbOnlyLi {
      resume(true)
      resume(false)
    }
  } with escapeContext { msg =>
    output = output.insert(0, msg)
    resume(())
  }
  return output
}


/// Gives the ability to translate sentences from the command line
def wrapper(): Unit = {
  with console;
  try {
    while (true) {
      var input = askQuestion("Enter a Toki Pona sentence or type 'q' to quit: ")
      if (input == "q") do escapeContext("")
      translateSentence(input).foreach { x => println(x) }
    }
  } with escapeContext { _ =>
    ()
  }
}

def safeToFile(): Unit = {
  try {
    var text = readFile("Data.txt").split("\n")
    var output = ""
    text.foreach { y => translateSentence(y).foreach { x => output = output ++ x ++ "\n" }}
    writeFile("Data.txt", output)
  }
  with Exception[IOError] {
    def raise(exception: IOError, msg: String) = {
      println(msg)
    }
  }
}


def main(): Unit = {
  safeToFile()
}