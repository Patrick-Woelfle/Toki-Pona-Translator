module trees // must be named same as the file!

import src/additionalLists
import src/effects

type SyntaxTree {
  Empty()
  Sentence(subjects: List[SyntaxTree], predicates: List[SyntaxTree])
  Predicate(verb: SyntaxTree, objects: List[SyntaxTree], prepositions: List[SyntaxTree])
  VerbLeaf(preverb: List[String], head: List[String], modifiers: List[String])
  ObjectLeaf(head: List[String], modifiers: List[String])
  SubjectLeaf(head: List[String], modifiers: List[String])
  PrepositionsLeaf(prep: List[String], head: List[String], modifiers: List[String])
}


def appendToSentence(tree: SyntaxTree, node: SyntaxTree): SyntaxTree = {
  tree match {
    case Sentence(subjects, predicates) => {
      node match {
        case SubjectLeaf(head, modifiers) => {
          return Sentence(subjects.append([node]), predicates)
        }
        case Predicate(verb, objects, prepositions) => {
          return Sentence(subjects, predicates.append([node]))
        }
        case _ => return Empty()
      }
    }
    case _ => return Empty()
  }
}



def printTree(node: SyntaxTree): Unit = {
  def printNode(node: SyntaxTree, indent: String): Unit = {
    node match {
      case Sentence(subjects, predicates) => {
        println(indent ++ "SENTENCE:")
        println(indent ++ "  " ++ "SUBJECTS:")
        subjects.foreach{ n => printNode(n, indent ++ "    ")}
        println(indent ++ "  " ++ "PREDICATES:")
        predicates.foreach{ n => printNode(n, indent ++ "    ")}
     }
      case SubjectLeaf(head, modifiers) => {
      println(indent ++ "SUBJECTLEAF: Head: " ++ showWords(head) ++ "  Modifiers: " ++ showWords(modifiers))
      }
      case ObjectLeaf(head, modifiers) => {
        println(indent ++ "OBJECTLEAF: Head: " ++ showWords(head) ++ "  Modifiers: " ++ showWords(modifiers))
      }
      case VerbLeaf(preverb, head, modifiers) => {
        println(indent ++ "VERBLEAF: Preverbs: " ++ showWords(preverb) ++ "  Head: " ++ showWords(head) ++ "  Modifiers: " ++ showWords(modifiers))
      }
      case PrepositionsLeaf(prep, head, modifiers) => {
        println(indent ++ "PREPOSITIONSLEAF: Preposition: " ++ showWords(prep) ++ "  Head: " ++ showWords(head) ++ "  Modifiers: " ++ showWords(modifiers))
      }
      case Predicate(verb, objects, prepositions) => {
        println(indent ++ "PREDICATE:")
        printNode(verb, indent ++ "  ")
        objects match {
          case Nil() => println(indent ++ "  " ++ "OBJECTLEAF: Empty")
          case Cons(_, _) => objects.foreach{ n => printNode(n, indent ++ "  ")}
        }
        prepositions match {
          case Nil() => println(indent ++ "  " ++ "PREPOSITIONSLEAF: Empty")
          case Cons(_, _) => prepositions.foreach{ n => printNode(n, indent ++ "  ")}
        }
      }
      case Empty() => {
        println(indent ++ "Empty")
      }
   }
  } 
  printNode(node, "")
}

def main(): Unit = {
  val tree1: SyntaxTree = Sentence([SubjectLeaf(["Soweli"], ["pimeja"]), SubjectLeaf(["Soweli"], ["pimeja"])], [Predicate(VerbLeaf([], ["lili"], ["suli", "pimeja"]), [Empty()], [PrepositionsLeaf(["lon"], [], [])])])
  var tree2: SyntaxTree = Sentence([], [])
  tree2 = appendToSentence(tree2, SubjectLeaf(["Soweli"], ["pimeja"]))
  //printTree(tree2.appendToSentence(SubjectLeaf(["Soweli"], ["pimeja"])))
  //printTree(tree2.appendToSentence(SubjectLeaf(["Soweli"], ["pimeja"])).appendToSentence(SubjectLeaf(["Soweli"], ["pimeja"])))
  //printTree(tree2.appendToSentence(Predicate(Empty(), [Empty()], [Empty()])))
  printTree(tree1)
}